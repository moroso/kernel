/* name/mod.mb: name server.  services requests for descriptors. 
 *
 * Author Amanda M. Watson
**/

use prelude::utils::list::*;
use prelude::locks::mutex::*;
use prelude::mallot::*;
use prelude::string::*;
use prelude::syslib::*;
use prelude::utils::asm::*;
use prelude::ipclib::*;
use prelude::thrlib::*;
use prelude::rt_abort;
use prelude::shared::ipc_defs::*;
use prelude::utils::asm::*;
use prelude::server::*;
use prelude::shared::ipc_defs::IPC_ERR_MIN;

const SERVER_ID: *u8 = "name_server";

const NAME_MAX: u32 = 64;
// id of name server
const NAME_SERVER: u32 = 0;

struct server_node {
    name: u8[NAME_MAX],
    desc: u32, 
    link: list_node,
}

struct name_request {
    name: u8[NAME_MAX],
    request_type: u32,
}

/*** request types ***/
const NAME_REGISTER: u32 = 0;
const NAME_REQUEST: u32 = 1;

/*** errors (start past ipc err min so we don't confuse the two) ***/
static NAME_ERR_MEM: i32 = IPC_ERR_MIN - 1;
static NAME_ERR_ARGS: i32 = IPC_ERR_MIN - 2;
static NAME_ERR_NOT_FOUND: i32 = IPC_ERR_MIN - 4;
static NAME_ERR_DUP: i32 = IPC_ERR_MIN - 5;

// list of server descs 
static server_list: list_head = LIST_HEAD_INIT!(server_list);
static server_lock: mut_t = MUTEX_INIT!();

static name_server_active: bool = false;

fn new_server_node(name: *u8, desc: u32) -> *server_node {
    let new_node = mallot(sizeof(server_node)) as *server_node;
    if (new_node == null) {
        return null;
    }

    strncpy(&(new_node->name)[0], name, NAME_MAX);
    new_node->desc = desc;
    list_init_node(&new_node->link);
    new_node
}


fn server_lookup(name: *u8) -> *server_node {
    let node: *server_node = null;
    mut_lock(&server_lock);
    list_foreach_entry!(node, &server_list, server_node, link, {
            if (strcmp(name, &(node->name)[0]) == 0) {
            mut_unlock(&server_lock);
            return node;
            }
            if (name[0] < node->name[0]) {
                break;
            }
            });

    mut_unlock(&server_lock);
    null
}

fn request_desc(name: *u8) -> i32 {
    let node = server_lookup(name);
    if (node == null) {
        return NAME_ERR_NOT_FOUND;
    } 
    let desc = node->desc;
    desc as i32 
}

fn server_list_insert(new_node: *server_node) -> i32 {
    mut_lock(&server_lock);
    let node: *server_node = null;
    list_foreach_entry!(node, &server_list, server_node, link, {
            if (strcmp(&(new_node->name)[0], &(node->name)[0]) == 0) {
            mut_unlock(&server_lock);
            return NAME_ERR_DUP;
            }
            if (node->name[0] > new_node->name[0]) {
            list_insert_before(&new_node->link, &node->link);
            mut_unlock(&server_lock);

            return 0;
            }
            });

    list_insert_tail(&new_node->link, &server_list);
    mut_unlock(&server_lock);

    0
} 

fn run_name_server(mb_arg: *u8) {
    /*** setup ***/
    let mb = mb_arg as i32;
    assert!(mb >= 0);

    if (server_thread_init(mb as u32) < 0) {
        server_thread_exit(&name_server_active, -1);
    }

    let status: i32 = 0; 
    let client_desc: i32 = -1;
    let request: name_request;
    let request_size = sizeof(name_request); 

    while(name_server_active) {
        request_size = sizeof(name_request);
        if (ipc_recv_mb(mb as u32, &client_desc, &request as *u8, &request_size, -1, null) >=
                0) {
            if (request_size != sizeof(name_request)) {
                status = -1;
                ipc_reply(null, 0, &status as *u32, sizeof(i32));

            } else {
                if (request.request_type == NAME_REGISTER) {
                    printf!("MESSAGE RECEIVED: %s\n", &request.name[0]);
                    let new_node = new_server_node(&request.name[0], client_desc as
                            u32);
                    if (new_node == null) {
                        status = -1;
                    } else if (server_list_insert(new_node) < 0) {
                        status = -2;
                    }
                    ipc_reply(null, 0, &status as *u32, sizeof(i32));


                    ipc_reply(null, 0, null, 0);
                } else if (request.request_type == NAME_REQUEST) {
                    let return_desc = request_desc(&(request.name)[0]);
                    if (return_desc < 0) {
                        status = return_desc;
                    }
                    ipc_reply_mb(&status as *u32, sizeof(i32), return_desc as u32); 
                }
            }
        } 

    }
    printf!("thread %d exiting server\n", get_tid());
    server_thread_exit(&name_server_active, 0);
}

fn name_server_init(num_threads: u32, return_desc: i32) -> i32 {
    server_init(SERVER_ID, &name_server_active, num_threads, run_name_server,
            return_desc, false, -1)
}

fn name_server_request(name: *u8, desc: *i32, timeout: i32) -> i32 {
    if (strnlen(name, NAME_MAX + 1) > NAME_MAX) {
        return NAME_ERR_ARGS;
    }

    let request: name_request;
    request.request_type = NAME_REQUEST;
    strncpy(&(request.name)[0], name, NAME_MAX);
    let status: i32 = -2;
    let status_size: u32 = sizeof(i32);
    let ret = ipc_request_mb(NAME_SERVER, &request as *u8, sizeof(name_request),
            &status as *u8, &status_size, desc, false, timeout);

    if (ret >= 0 && status_size == sizeof(u32)) {
        return status;
    }
    ret


}

fn name_server_register(name: *u8, desc: u32, timeout: i32) -> i32 {
    if (strnlen(name, NAME_MAX + 1) > NAME_MAX) {
        return NAME_ERR_ARGS;
    }

    let request: name_request;
    request.request_type = NAME_REGISTER;
    strncpy(&(request.name)[0], name, NAME_MAX);
    let status: i32 = -2;
    let status_size: u32 = sizeof(i32);
    let ret = ipc_forward_mb(NAME_SERVER, desc, &request as *u8, sizeof(name_request),
            &status as *u8, &status_size, false, timeout);

    if (ret >= 0 && status_size == sizeof(u32)) {
        return status;
    }

    ret
}
