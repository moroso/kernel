/* mailbox.mb contains functions and declarations for mailbox structure.  The
 * mailbox allows owners to receive messages, and non-owners to send them.  When
 * a reply is expected, the mailbox saves the sender's data in the receiving tcb
 * for future wakeup (either on end, or on reply)
 *
 * A mailbox is owned by the proc that creates it: all threads in that proc can
 * receive on the mailbox.  All other threads will return with error if they
 * attempt to receive
 *
 * We can think of the mailbox, to some extent, as the middle layer between the
 * rend primitive and IPC 
 *
 * Author: Amanda M. Watson
*/
use locks::rendezvous::*;
use structures::proc::*;
use structures::schedule::*;
use ipc::desc_table::*;
use utils::slab_alloc::*;
use utils::list::*;
use structures::tcb::*;
use entry::get_ticks;

// the error that's returned if we attempt to wait on a mailbox we do not own
const ERR_OWNER: i32 = -7;
const ERR_INTERNAL: i32 = -8;

struct mailbox_t {
    // primitive that synchronizes senders
    // and receivers
    mb_rend: rend,
    // denotes the proc that owns the mailbox. only threads that belong to this
    // proc will be allowed to receive messages from the mailbox 
    owner: *proc,
    ref_count: u32, // denotes how many desc tables have references
    // denotes whether the mailbox is accepting activity
    active: bool, 
}

/* allocates a new mailbox, and assigns proc of invoking thread as its owner.
 * Assumes owner has corresponding proc
 * Returns mailbox descriptor on success, negative error code on OOM failure, or
 * if descriptor table is full */
fn mailbox_new() -> i32 {
    let mb = slub_alloc(sizeof(mailbox_t)) as *mailbox_t; 
    if (mb == null) {
        return -2;
    }
    let ret: i32 = mb_init(mb, get_tcb()->proc);
    ret
}

// create mailbox, add own proc as owner, add to desc table.
// returns error if there is no room to add the descriptor
// otherwise, returns new descriptor
fn mb_init(mb: *mailbox_t, proc: *proc) -> i32 {
    assert!(proc != null);
    rend_init(&(mb->mb_rend));
    mb->owner = proc; 
    mb->active = true;
    mb->ref_count = 0;
    let ret = mb_table_add(&proc->mb_desc, mb);
    assert!(mb->ref_count == 1);
    ret
}

fn mb_recv(mb: *mailbox_t, message: *u32, message_len: u32, message_recv: **u32,
        recv_len: *u32, timeout: u32, err: *i32) -> *tcb {

    if (!mb->active) {
        *err = ERR_INTERNAL;
        return null;
    }

    assert!(list_is_empty(&get_tcb()->last_send));

    if (get_tcb()->proc != mb->owner) {
        *err = ERR_OWNER;         
        return null;
    }
    let ret = rend_wait(&mb->mb_rend, message, message_len, message_recv, recv_len, timeout,
            RECV, err);

    if (ret != null) {
        list_insert_head(&ret->rend_link, &(get_tcb()->last_send));
        assert!(ret->can_timeout == false || ret->timeout ==
                -1);
        assert!(ret->message_len > 0 && ret->message != null);
        *recv_len = ret->message_len;
        *message_recv = ret->message;
        return ret->tcb;
    } else {
        *recv_len = 0;
        *message_recv = null;
    }
    null
}

fn mb_end_recv(err: i32) {

    if (list_is_empty(&get_tcb()->last_send)) {
        return ();
    }

    let last_send: *rend_node = list_head_entry!(&get_tcb()->last_send, rend_node,
            rend_link);

    if (last_send->option != SEND_SYNC_REPLY || err < 0) {
        assert!(last_send->can_timeout == false);
        list_del(&last_send->rend_link);
        rend_signal(null, 0, last_send, err);
    } else {
        assert!(last_send->tcb != null);
        assert!(last_send->can_timeout == false || last_send->timeout ==
                -1);
        last_send->can_timeout = true;
    }

}

fn mb_send(mb: *mailbox_t, message: *u32, message_len: u32, message_recv: **u32,
        recv_len: *u32, timeout: u32, option: u32, err: *i32) -> *tcb {

    if (!mb->active) {
        *err = ERR_INTERNAL;
        return null;
    }

    if (timeout != -1) { 
        timeout = timeout + get_ticks();
    }

    let ret = rend_wait(&mb->mb_rend, message, message_len, message_recv, recv_len, timeout,
            option, err);

    if (ret != null) {
        list_insert_head(&ret->rend_link, &(get_tcb()->last_send));
        assert!(ret->can_timeout == false || ret->timeout ==
                -1);
        assert!(ret->message_len > 0 && ret->message != null);
        *recv_len = ret->message_len;
        *message_recv = ret->message;
        return ret->tcb;
    } else {
        *recv_len = 0;
        *message_recv = null;
    }

    null
}

fn mb_end_send(message: *u32, message_len: u32, err: i32) {
    if (list_is_empty(&get_tcb()->last_send)) {
        assert!(message_len == 0);
        return ();
    }

    let last_send: *rend_node = list_head_entry!(&get_tcb()->last_send, rend_node,
            rend_link);
    assert!(last_send->option == RECV);
    assert!(last_send->can_timeout == false);
    list_del(&last_send->rend_link);
    rend_signal(message, message_len, last_send, err);

}

fn mb_destroy(mb: *mailbox_t) {
    mb->active = false;
    rend_destroy(&mb->mb_rend, REND_ERR_DEAD);
    assert!(mb->ref_count == 0);
    mb->owner = null; 
    slub_free(mb as *u32, sizeof(mailbox_t));
}
